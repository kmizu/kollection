buildscript {
  ext.kotlin_version = '1.1.2'
  ext.dokka_version = '0.9.15'
  ext.spek_version = '1.1'
  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "http://repository.jetbrains.com/all"
    }
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
  }
}

apply plugin: "org.jetbrains.dokka"
apply plugin: 'kotlin'

project(":") {
  apply plugin: "application"
  apply plugin: "idea"
  apply plugin: 'maven'
  apply plugin: 'signing'
  mainClassName = "com.github.kmizu.kollection.example.MainKt"
  group = "com.github.kmizu"
  version = "0.4"
  ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
  signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
  }

  dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
  }

  repositories {
    mavenCentral()
    maven {
      url "http://oss.sonatype.org/content/repositories/snapshots"
    }
    maven {
      url "http://repository.jetbrains.com/all"
    }
  }
  dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.spek:spek:$spek_version"
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
  classifier = 'javadoc'
  from dokka.outputDirectory
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.2'
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        if(rootProject.hasProperty('ossrhUsername')  && rootProject.hasProperty('ossrhPassword')) {
          authentication(userName: rootProject.ossrhUsername, password: rootProject.ossrhPassword)
        }
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        if(rootProject.hasProperty('ossrhUsername')  && rootProject.hasProperty('ossrhPassword')) {
          authentication(userName: rootProject.ossrhUsername, password: rootProject.ossrhPassword)
        }
      }

      pom.project {
        name 'kollection'
        packaging 'jar'
        description "A parser combinator library written in Kotlin"
        licenses {
          license {
            name "The MIT License"
              url "http://www.opensource.org/licenses/MIT"
          }
        }
        url "https://github.com/kmizu/kollection"
        scm {
          url "git@github.com:kmizu/kollection.git"
          connection "scm:git:git@github.com:kmizu/kollection.git"
        }
        developers {
          developer {
            name "Kota Mizushima"
            url "https://github.com/kmizu"
            email "mizukota@gmail.com"
          }
        }
      }
    }
  }
}
repositories {
  mavenCentral()
}
dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}
sourceSets {
  main.java.srcDirs += 'src/main/kotlin'
}

